---
layout: base
---

<script type="text/javascript">
 lexemeSparql = `
# Data for a specific lexeme
SELECT DISTINCT
  ?description
  (?valueLabel As ?value_) ?value_Url
  ?source ?sourceUrl
WHERE {
  VALUES ?lexeme { wd:${ l } }
  {
    BIND(1 AS ?order)
    BIND("Lemma" AS ?description)
    {SELECT (GROUP_CONCAT(?valuelabel; separator=" // ") AS ?valueLabel) WHERE {     
       wd:${ l } wikibase:lemma ?valuelabel . 
    }} 
  }
  UNION 
  {
    BIND(2 AS ?order)
    BIND("Language" AS ?description)
    ?lexeme dct:language ?value .
    ?value rdfs:label ?valueLabel .
    FILTER (LANG(?valueLabel) = 'en')
    BIND(CONCAT("../language?q=", SUBSTR(STR(?value), 32),"&qLabel=",?valueLabel)  AS ?value_Url)
  }
  UNION 
  {
    BIND(3 AS ?order)
    BIND("Dialect" AS ?description)
    ?lexeme wdt:P7481 ?value_Url .
    ?value_Url rdfs:label ?valueLabel .
    FILTER (LANG(?valueLabel) = 'en')
  }
  UNION 
  {
    BIND(4 AS ?order)
    BIND("Lexical category" AS ?description)
    ?lexeme wikibase:lexicalCategory ?value .
	?lexeme dct:language ?langvalue .
	?langvalue rdfs:label ?langvalueLabel .
    FILTER (LANG(?langvalueLabel) = 'en')
    ?value rdfs:label ?valueLabel .
    FILTER (LANG(?valueLabel) = 'en')
    BIND(CONCAT("../lexical-category?q=",?value,"&qLabel=",?valueLabel,"&l=",?langvalue,"&lLabel=",?langvalueLabel)  AS ?value_Url)
  }
  UNION 
  {
    BIND(5 AS ?order)
    BIND("Instance of" AS ?description)
    ?lexeme p:P31 ?value_statement .
    ?value_statement ps:P31 ?value .
    ?value rdfs:label ?valueLabel .
    FILTER (LANG(?valueLabel) = 'en')
    BIND(CONCAT("../property_value?p=P31&q=", SUBSTR(STR(?value), 32),"&qLabel=",?valueLabel)  AS ?value_Url)
    OPTIONAL {
      ?value_statement prov:wasDerivedFrom / pr:P248 ?source_ .
      ?source_ rdfs:label ?source .
      FILTER (LANG(?source) = 'en')
      BIND(CONCAT("../reference?q=", SUBSTR(STR(?source_), 32))  AS ?sourceUrl)
    }
  }
  UNION 
  {
    BIND(6 AS ?order)
    BIND("Word stem" AS ?description)
    ?lexeme wdt:P5187 ?value .
    BIND(?value AS ?valueLabel)
  }
  UNION 
  {
    BIND(7 AS ?order)
    BIND("Based on" AS ?description)
    ?lexeme wdt:P144 ?value .
    BIND(?value AS ?valueLabel)
  }UNION 
  {
    BIND(8 AS ?order)
    BIND("Paradigm" AS ?description)
    ?lexeme wdt:P5911 ?value_Url .
    ?value_Url rdfs:label ?valueLabel .
    FILTER (LANG(?valueLabel) = 'en')
  }
  UNION 
  {
    BIND(9 AS ?order)
    BIND("Root" AS ?description)
    {SELECT ?description (GROUP_CONCAT(?valueurl; separator=" // ") AS ?value_Url) (GROUP_CONCAT(?valuelabel; separator=" // ") AS ?valueLabel) WHERE {     
	  BIND("Root" AS ?description)
	  wd:${ l } wdt:P5920 ?value .
	  ?value wikibase:lemma ?valuelabel .
	  BIND(CONCAT("../l?qLabel=",?valuelabel,"&l=",SUBSTR(STR(?value), 32))  AS ?valueurl)
    }GROUP BY ?description }  
  }
  UNION 
  {
    BIND(10 AS ?order)
    BIND("Grammatical gender" AS ?description)
    ?lexeme wdt:P5185 ?value_Url .
    ?value_Url rdfs:label ?valueLabel .
    FILTER (LANG(?valueLabel) = 'en')
  }
  UNION 
  {
    BIND(11 AS ?order)
    BIND("Conjugation class" AS ?description)
    ?lexeme wdt:P5186 ?value .
    ?value rdfs:label ?valueLabel .
    FILTER (LANG(?valueLabel) = "en")
    BIND(CONCAT("../property_value?p=P5186&q=", SUBSTR(STR(?value), 32),"&qLabel=",?valueLabel)  AS ?value_Url)

  }
  UNION 
  {
    BIND(12 AS ?order)
    BIND("Auxiliary verb" AS ?description)
    ?lexeme wdt:P5401 ?value .
    ?value wikibase:lemma ?valueLabel .
    # BIND(CONCAT("../property_value?p=P5401&q=", SUBSTR(STR(?value), 32),"&qLabel=",?valueLabel)  AS ?value_Url)
    BIND(SUBSTR(STR(?value), 32) AS ?value_Url)
  }
  UNION 
  {
    # Characterized by
    BIND(13 AS ?order)
    BIND("Characterized by" AS ?description)
    wd:${ l } wdt:P1552 ?value .
    ?value rdfs:label ?valueLabel .
    FILTER (LANG(?valueLabel) = "en")
    BIND(CONCAT("../property_value?p=P1552&q=", SUBSTR(STR(?value), 32),"&qLabel=",?valueLabel)  AS ?value_Url)
  }
  UNION 
  {
    # Derived from
    BIND(14 AS ?order)
    {SELECT ?description (GROUP_CONCAT(?valueurl; separator=" // ") AS ?value_Url) (GROUP_CONCAT(?valuelabel; separator=" // ") AS ?valueLabel) WHERE {     
	  BIND("Derived from" AS ?description)
	  wd:${ l } wdt:P5191 ?value .
	  ?value wikibase:lemma ?valuelabel .
	  BIND(CONCAT("../l?qLabel=",?valuelabel,"&l=",SUBSTR(STR(?value), 32))  AS ?valueurl)
    }GROUP BY ?description }  
  }
  UNION 
  {
    # Homographs
    BIND(15 AS ?order)
    {SELECT ?description (GROUP_CONCAT(?valueurl; separator=" // ") AS ?value_Url) (GROUP_CONCAT(?valuelabel; separator=" // ") AS ?valueLabel) WHERE {     
      BIND("Homographs" AS ?description)
	  wd:${ l } wdt:P5402 ?value .
      ?value wikibase:lemma ?valuelabel .
      BIND(CONCAT("../l?qLabel=",?valuelabel,"&l=",SUBSTR(STR(?value), 32))  AS ?valueurl)
    }GROUP BY ?description }  
  }
  UNION 
  {
    # Transitivity
    BIND(16 AS ?order)   
	BIND("Transitivity" AS ?description)
    wd:${ l } wdt:P9295 ?value .
    ?value rdfs:label ?valueLabel .
    FILTER (LANG(?valueLabel) = "en")
    BIND(CONCAT("../property_value?p=P9295&q=", SUBSTR(STR(?value), 32),"&qLabel=",?valueLabel)  AS ?value_Url)
  }
  UNION 
  {
    # Time Period
    BIND(17 AS ?order)
    {SELECT ?description (GROUP_CONCAT(?valueurl; separator=" // ") AS ?value_Url) (GROUP_CONCAT(?valuelabel; separator=" // ") AS ?valueLabel) WHERE {     
	  BIND("Time Period" AS ?description)
	  wd:${ l } wdt:P2348 ?value .
	  ?value rdfs:label ?valuelabel .
	  FILTER (LANG(?valuelabel) = 'en')
	  BIND(CONCAT("../timeperiod?qLabel=",?valuelabel,"&q=",SUBSTR(STR(?value), 32))  AS ?valueurl)
    }GROUP BY ?description }  
  }
  UNION 
  {
    BIND(18 AS ?order)
	{SELECT ?description (GROUP_CONCAT(DISTINCT ?valueurl; separator=" + ") AS ?value_Url) (GROUP_CONCAT(?valuelabel; separator=" + ") AS ?valueLabel) WHERE {     
		BIND("Compound" AS ?description)		
        {SELECT ?value (GROUP_CONCAT(DISTINCT ?forml; separator=" // ") AS ?formlabel) ?comb WHERE {
          wd:${ l } p:P5238 ?comb . 
          ?comb ps:P5238 ?value .
          ?comb pq:P1545 ?seriesordinal . 
		  ?comb pq:P5548 ?form . 
		  ?form ontolex:representation ?forml . 
        }
        GROUP BY ?value ?comb
        ORDER BY xsd:integer(?seriesordinal)
        }
		?comb pq:P5980 ?combsense . 
		?combsense wdt:P5137|wdt:P9970 ?combsenseval . 
		?combsense skos:definition ?combsensedeff .
		FILTER (LANG(?combsensedeff) = 'en')
		#wdt:P5238+ ?value .
		#?value wikibase:lemma ?valuelabel .
		BIND(CONCAT(?formlabel," (",?combsensedeff,")") AS ?valuelabel)
		BIND(CONCAT("../l?&qLabel=",?valuelabel,"&l=",SUBSTR(STR(?value), 32))  AS ?valueurl)
    }GROUP BY ?description }
  }
  UNION 
  {
    BIND(20 AS ?order)
    BIND("Described in" AS ?description)
    ?lexeme ontolex:lexicalForm? / p:P1343 ?value_statement .
    ?value_statement ps:P1343 ?value .
    ?value rdfs:label ?valueLab .
	OPTIONAL { ?value_statement pq:P478 ?refvol. }
	OPTIONAL{?value_statement pq:P1810 ?valuenum .}
	OPTIONAL{?value_statement pq:P7421 ?refline .}
	OPTIONAL{?value_statement pq:P304 ?refpage .}
    BIND(COALESCE(CONCAT(?valuenum, " "), "") As ?thenum)
    BIND(COALESCE(CONCAT("v. ", ?refvol, " "), "") AS ?thevol)
    BIND(COALESCE(CONCAT("p. ",?refpage), "") As ?thepage)
    BIND(COALESCE(CONCAT(" l. ",?refline), "") As ?theline)
	BIND(CONCAT(?thenum, ?thevol, ?thepage, ?theline) AS ?refnumber)
    FILTER (LANG(?valueLab) = 'en')
    BIND(CONCAT(?valueLab," [",?refnumber,"]") AS ?valueLabel)
    BIND(CONCAT("../reflistlang?q=", SUBSTR(STR(?value), 32),"&qLabel=",STR(?valueLabel))  AS ?value_Url)
    OPTIONAL {
      ?value_statement prov:wasDerivedFrom / pr:P854 ?sourceUrl .
      BIND("↗" AS ?source)
    }
  }
  UNION 
  {
    BIND(30 AS ?order)
    BIND("First attested from" AS ?description)
    ?lexeme p:P6684 ?value_statement .
    ?value_statement ps:P6684 ?value .
    ?value rdfs:label ?valueLabel .
    FILTER (LANG(?valueLabel) = "en")
    BIND(CONCAT("../reference?q=", SUBSTR(STR(?value), 32))  AS ?value_Url)
    OPTIONAL {
      ?value_statement prov:wasDerivedFrom / pr:P854 ?sourceUrl .
      OPTIONAL { ?value_statement pq:P577 ?date }
      BIND(COALESCE(
        CONCAT(STR(YEAR(?date)), "-", STR(MONTH(?date)), "-", STR(DAY(?date))),"↗") AS ?source)
    }
  }
  UNION 
  {
    BIND(31 AS ?order)
    BIND("Attested in" AS ?description)
    ?lexeme ontolex:lexicalForm? / p:P5323 ?value_statement .
    ?value_statement ps:P5323 ?value .
    ?value rdfs:label ?valueLabel .
    FILTER (LANG(?valueLabel) = "en")
    BIND(CONCAT("../reference?q=", SUBSTR(STR(?value), 32))  AS ?value_Url)
    OPTIONAL {
      ?value_statement prov:wasDerivedFrom / pr:P854 ?sourceUrl .
      OPTIONAL { ?value_statement pq:P577 ?date }
      BIND(COALESCE(
	  CONCAT(STR(YEAR(?date)), "-", STR(MONTH(?date)), "-", STR(DAY(?date)), " ↗"),
        "↗") AS ?source)
    }
    OPTIONAL {
      ?value wdt:P577 ?date . 
      BIND(CONCAT(STR(YEAR(?date)), "-", STR(MONTH(?date)), "-", STR(DAY(?date))) AS ?source)
    }

  }
  UNION 
  {
	BIND(32 AS ?order)
	BIND("Usage example" AS ?description)
	{ SELECT ?valueLabel ?value_Url (GROUP_CONCAT(?sourcee; separator=" // ") AS ?source) (GROUP_CONCAT(?sourceurl; separator=" // ") AS ?sourceUrl) WHERE {
		wd:${ l } p:P5831 ?value_statement .
		?value_statement ps:P5831 ?value .
		BIND(CONCAT("../text-to-lexemes?text=", ENCODE_FOR_URI(?value), "&text-language=", LANG(?value)) AS ?value_Url)
		OPTIONAL{
		  ?value_statement pq:P2441 ?literal_translation .
		  FILTER(lang(?literal_translation)="en")  
		}
		BIND(CONCAT(?value,COALESCE(CONCAT(" [",?literal_translation,"]"),"")) AS ?valueLabel)
		OPTIONAL {
		  ?value_statement prov:wasDerivedFrom ?deriv .
		  ?deriv pr:P248 ?source_ . 
		  OPTIONAL {?deriv pr:P478 ?refvol. }
		  OPTIONAL{?deriv pr:P1810 ?valuenum .}
		  OPTIONAL{?deriv pr:P7421 ?refline .}
		  OPTIONAL{?deriv pr:P304 ?refpage .}
		  BIND(COALESCE(CONCAT(?valuenum, " "), "") As ?thenum)
		  BIND(COALESCE(CONCAT("v. ", ?refvol, " "), "") AS ?thevol)
		  BIND(COALESCE(CONCAT("p. ",?refpage), "") As ?thepage)
		  BIND(COALESCE(CONCAT(" l. ",?refline), "") As ?theline)
		  BIND(CONCAT(?thenum, ?thevol, ?thepage, ?theline) AS ?refnumber)
		  ?source_ rdfs:label ?titlee .
		  FILTER (LANG(?titlee) = 'en')
		  BIND(CONCAT(?titlee," [",?refnumber,"]") AS ?title)    
		  BIND(COALESCE(?title, "Ref.") AS ?sourcee)
		  BIND(CONCAT("../reference?q=", SUBSTR(STR(?source_), 32)) AS ?sourceurl)
		}  	
	}GROUP BY ?valueLabel ?value_Url
  }
  }
}
ORDER BY ?order
`

idSparql= `
# List of ids for a specific character
SELECT DISTINCT
  ?idprop ?idpropLabel (CONCAT(?formaturl,?id) AS ?idUrl) ?id
WHERE {
  VALUES ?lexeme { wd:${ l } }
  ?lexeme ?idprop ?id .
  ?idpropp wikibase:directClaim ?idprop ; wikibase:propertyType wikibase:ExternalId .
  OPTIONAL {
    ?idpropp wdt:P1630 ?formatur .
    BIND(REPLACE(STR(?formatur), "\\\\$1", "", "i") AS ?formaturl)
  }
  ?idpropp rdfs:label ?idpropLabel .
  FILTER(lang(?idpropLabel)="en")
}
`


 formsSparql = `
 # List of forms for a specific lexeme
 SELECT
  ?form (GROUP_CONCAT(DISTINCT ?formlabel; separator=" // ") AS ?formLabel)
   (GROUP_CONCAT(DISTINCT ?formlabellang; separator=" // ") AS ?language)
   (GROUP_CONCAT(DISTINCT ?feature; separator=" // ") AS ?features)
   (GROUP_CONCAT(DISTINCT ?featureLabel; separator=" // ") AS ?featuresLabel)   
# (GROUP_CONCAT(DISTINCT ?hyphenation; separator=" // ") AS ?hyphenations)
 WHERE {
   wd:${ l } ontolex:lexicalForm ?form .
   ?form ontolex:representation ?formlabel .
   BIND(lang(?formlabel) AS ?formlabellang)
   OPTIONAL {
     ?form wikibase:grammaticalFeature ?feature .
     ?feature rdfs:label ?featureLabel .
     FILTER (LANG(?featureLabel) = "en")
   }
   OPTIONAL {
    ?form wdt:P5279 ?hyphenation
   }
 }
 GROUP BY ?form
 ORDER BY (STR(?form))
 `

 sensesSparql = `
# List of senses for a specific lexeme
 SELECT
  ?sense ?senseLabel ?glossUrl
  (GROUP_CONCAT(DISTINCT ?glloss; separator=" // ") AS ?gloss)
  (IRI(CONCAT("https://commons.wikimedia.org/w/index.php?title=Special:Redirect/file&width=200&wpvalue=", 
          SUBSTR(STR(SAMPLE(?images)), 52))) AS ?sense_image)
 WHERE {
   wd:${ l } ontolex:sense ?sense .
   BIND(SUBSTR(STR(?sense), 32) AS ?senseLabel)
   ?sense skos:definition ?glosss .
   OPTIONAL {
     ?sense wdt:P5137|wdt:P9970 ?glossUrl .
   }
   OPTIONAL {
   {
     ?sense wdt:P18 ?images
   }UNION {
	?sense wdt:P5137|wdt:P9970 ?glossUrl .
	?glossUrl wdt:P18 ?images .
   }
   }
   BIND(CONCAT(?glosss,"@",lang(?glosss)) AS ?glloss)
 }
 GROUP BY ?sense ?senseLabel ?glossUrl
 ORDER BY (STR(?sense))
`

translationSparql=`
SELECT DISTINCT (GROUP_CONCAT(DISTINCT ?lexemea; separator=" // ") as ?lexemeLabel) 
    ?lexcat ?lexcatLabel
	(GROUP_CONCAT(DISTINCT ?senseval; separator=" // ") as ?sense)
	(GROUP_CONCAT(DISTINCT ?senselabel; separator=" // ") as ?senseLabel)    
    ?translation ?translationLabel ?language ?languageLabel ?translationlexeme 
    (GROUP_CONCAT(DISTINCT ?lexeme2a; separator=" // ") as ?translationlexemeLabel) 
	WHERE {	
	  wd:${ l } rdf:type ontolex:LexicalEntry ;           
		 dct:language ?lexlang ;		   
		 wikibase:lemma ?lexemea ;
		 wikibase:lexicalCategory ?lexcat .
	  ?lexcat rdfs:label ?lexcatLabel . FILTER(LANG(?lexcatLabel)= "en")
      wd:${ l } ontolex:sense ?sense .
      {
        wd:${ l } wdt:P11577 ?translationlexeme . ?translationlexeme ontolex:sense ?translation .
      }UNION
      {
        ?sense wdt:P5972 ?translation . 
        ?translationlexeme ontolex:sense ?translation .
      }
	  ?translation skos:definition ?translationLabel .  FILTER(LANG(?translationLabel)= "en") 
      ?sense skos:definition ?gloss .  FILTER(LANG(?gloss)= "en") 
      OPTIONAL {?sense wdt:P5137|wdt:P9970 ?senseval . ?senseval rdfs:label ?senselab .  FILTER(LANG(?senselab)= "en")}
      BIND(CONCAT(?gloss, COALESCE(CONCAT(" [", ?senselab, " (", REPLACE(STR(?senseval), "http://www.wikidata.org/entity/", ""), ")", "]"),"")) AS ?senselabel)   
      ?translationlexeme wikibase:lemma ?lexeme2a .
      ?translationlexeme dct:language ?language .	  
      OPTIONAL{?translationlexeme wdt:P7481 ?dialect . ?dialect rdfs:label ?dialectLabel . FILTER(LANG(?dialectLabel)= "en") }
      ?language rdfs:label ?languageeLabel .
      FILTER(LANG(?languageeLabel)= "en")
      BIND(CONCAT(?languageeLabel,COALESCE(CONCAT(" <",?dialectLabel,">"),"")) AS ?languageLabel)
      }
    GROUP BY ?lexeme ?lexcat ?lexcatLabel ?translation ?translationLabel ?language ?languageLabel ?translationlexeme
	ORDER BY ?lexemeLabel
`

compoundsSparql = `

SELECT
  ?lexeme (GROUP_CONCAT(?lexemelabel; separator=" // ") AS ?lexemeLabel) 
  ?lexical_category ?lexical_categoryLabel ?compounds ?compoundsLabel
  (GROUP_CONCAT(DISTINCT ?sensse; SEPARATOR = " // ") AS ?senses) 
(GROUP_CONCAT(DISTINCT ?senseLabell; SEPARATOR = " // ") AS ?sensesLabel)
WHERE {
  {
    SELECT
      ?lexeme
      (GROUP_CONCAT(?compound; separator=" // ") AS ?compounds)
      (GROUP_CONCAT(DISTINCT ?compoundLabel; separator=" // ") AS ?compoundsLabel)
    WHERE {
	  SELECT ?lexeme ?compound ?compoundLabel ?sensedef WHERE {
      ?lexeme wdt:P5238 wd:L748857 .
      OPTIONAL {
        ?lexeme p:P5238 ?compoundstat .
        ?compoundstat ps:P5238 ?compound .
        ?compound wikibase:lemma ?compoundlabel .
        #FILTER (?compound != wd:L703986)
		?compoundstat pq:P1545 ?seriesordinal .
        OPTIONAL {
          ?compoundstat pq:P5980 ?sense .
          ?sense skos:definition ?sensedeff .
		  FILTER(LANG(?sensedeff) = "en")		  
        }   
        BIND(COALESCE(CONCAT(" [",?sensedeff,"]"), "") AS ?sensedef)
        BIND(CONCAT(?compoundlabel,?sensedef) AS ?compoundLabel)
      }
	}
    ORDER BY xsd:integer(?seriesordinal)
    }
    GROUP BY ?lexeme ?sensedefs
  }UNION{
    SELECT ?lexeme (GROUP_CONCAT(?lexeme; separator=" // ") AS ?compounds)
      (GROUP_CONCAT(DISTINCT ?compoundLabel; separator=" // ") AS ?compoundsLabel) WHERE {
      ?lexeme wdt:P5191 wd:L748857 .
      ?lexeme wikibase:lemma ?compoundlabel .
      OPTIONAL {
          ?lexeme ontolex:sense ?sense .
          ?sense skos:definition ?sensedeff .
		  FILTER(LANG(?sensedeff) = "en")		  
      }  
      BIND(COALESCE(CONCAT(" [",?sensedeff,"]"), "") AS ?sensedef)
      BIND(CONCAT(?compoundlabel,?sensedef) AS ?compoundLabel)
    }GROUP BY ?lexeme
  }
  
  ?lexeme wikibase:lemma ?lexemelabel .
  OPTIONAL {
	  ?lexeme wikibase:lexicalCategory ?lexical_category .
	  ?lexical_category rdfs:label ?lexical_categoryLabel .
	  FILTER (LANG(?lexical_categoryLabel) = 'en')
  }
  OPTIONAL {
    ?lexeme ontolex:sense ?senseitem.
    ?senseitem skos:definition ?gloss.
    OPTIONAL {
      ?senseitem wdt:P5137|wdt:P9970 ?sensee.
      ?sensee rdfs:label ?senseLab.
      FILTER(LANG(?senseLab) = "en")
    }
    FILTER(LANG(?gloss) = "en")
    BIND(COALESCE(?sensee, "") AS ?sensse)
    BIND(CONCAT(?gloss, COALESCE(CONCAT(" [", ?senseLab, " (", REPLACE(STR(?sensee), "http://www.wikidata.org/entity/", ""), ")", "]"),"")) AS ?senseLabell)
  }
}
GROUP BY ?lexeme ?lexical_category ?lexical_categoryLabel ?compounds ?compoundsLabel
`


 $(document).ready(function() {
     sparqlToDataTable(lexemeSparql, "#lexeme",
		       options={
			   linkPrefixes: {
			       source: '../reference',
				   value_Url: '../'
			   },
			   paging: false,
			   sDom: 'rt',
		       });
     sparqlToDataTable(idSparql, "#ids",
		       options={divElem: "#identifier-q", paging: false, sDom: 'rt',
			   linkPrefixes: {
			       idUrl: ''
			   }});
     sparqlToDataTable(translationSparql, "#translations",
		       options={divElem: "#translation-q", paging: false, sDom: 'rt',
			   linkPrefixes: {
			       idUrl: ''
			   }});
	 sparqlToDataTable(formsSparql, "#forms",
		       options={paging: false, sDom: 'rt',
			   linkPrefixes: {
			       form: '../lf'
			   }});
     sparqlToDataTable(sensesSparql, "#senses",
		       options={paging: false, sDom: 'rt',
			   linkPrefixes: {
			       sense: '../ls',
				   gloss: ''
			   }});
     sparqlToDataTable(compoundsSparql, "#compounds",
		       options={divElem: "#compounds-q", paging: true, sDom: 'rt',});
 });

</script>

 <div class="l-entry">

     <h1>Lexeme: ${ qLabel } (${ l })</h1>
     
     
     <p>
         <a title="Back"
	    role="button"
	    class="btn btn-outline-dark btn-sm"
	    href="../language?q=${ qb }&qLabel=${ qbLabel }">←</a>
	 <a title="View and edit lexeme in Wikidata"
	    role="button"
	    class="btn btn-outline-dark btn-sm"
	    href="https://www.wikidata.org/wiki/Lexeme:${ l }">Wikidata ↗</a>
     </p>
     
     <table class="table table-hover" id="lexeme"></table>
     
     <div class="container">
	 <div class="row">
	      <div class="col-12 col-lg-6">
		  <table class="table table-hover" id="forms"></table>
	      </div>
	      <div class="col-12 col-lg-6">
		  <table class="table table-hover" id="senses"></table>
	      </div>
	 </div>
     </div>

	 <div id="identifier-q">
	 <h2>Identifier</h2>
     <table class="table table-hover" id="ids"></table>
     </div>
    <hr/>
    <div id="translation-q">
	 <h2>Translations and equivalents of Lexeme ${ qLabel } (${ l })</h2>
     <table class="table table-hover" id="translations"></table>
     </div>
       <div id="compounds-q">
	<h2>Compounds or derivations with Lexeme ${ qLabel } (${ l })</h2>

    <table class="table table-hover" id="compounds"></table>
	</div>
    <h2>Compound, <span style="color:#778899">translations</span> and derivation graph</h2>
    <button class="btn btn-outline-dark btn-sm glyphicon glyphicon-fullscreen" onclick="toggleFullScreen('wikidataembed')">FullScreen</button>
    <div class="embed-responsive embed-responsive-4by3">  
	  <iframe loading="lazy" id="wikidataembed" allow="fullscreen" class="embed-responsive-item" src="https://query.wikidata.org/embed.html#%23%20Show%20compound%20and%20derivation%20hierarchy%0A%23defaultView%3AGraph%0ASELECT%20%3Flexeme1%20%3Flexeme1Label%20%3Flexeme1Image%20%3Flexeme1Trans%20%3Flexeme1TransLabel%20%3Frgb%20%3Flexeme2%20%3Flexeme2Label%20%3Flexeme2Image%20%3Flexeme2Trans%20%3Flexeme2TransLabel%20%20%0AWHERE%20%7B%0A%7B%0A%0ASELECT%20DISTINCT%0A%20%20%3Flexeme1%20(GROUP_CONCAT(DISTINCT%20%3Flexeme1Labell%3B%20separator%3D%22%20%2F%2F%20%22)%20AS%20%3Flexeme1Llabel)%20%3Frgb%20%3Flexeme1gloss%20%3Flexeme1langLabel%20%3Flexeme1Image%20%3Flexeme1Trans%0A%20%20%20%20%20%20%20%20%20%20%20(GROUP_CONCAT(DISTINCT%20%3Flexeme1TranssLemma%3B%20separator%3D%22%20%2F%2F%20%22)%20AS%20%3Flexeme1TransLabell)%20%3Flexeme1TransLabelBP%0A%20%20%3Flexeme2%20(GROUP_CONCAT(DISTINCT%20%3Flexeme2Labell%3B%20separator%3D%22%20%2F%2F%20%22)%20AS%20%3Flexeme2Llabel)%20%3Flexeme2gloss%20%3Flexeme2langLabel%20%3Flexeme2Image%20%3Flexeme2Trans%20%0A%20%20%20%20%20%20%20%20%20%20%20(GROUP_CONCAT(DISTINCT%20%3Flexeme2TranssLemma%3B%20separator%3D%22%20%2F%2F%20%22)%20AS%20%3Flexeme2TransLabell)%20%3Flexeme2TransLabelBP%20%3Flexeme1DiaLabel%20%3Flexeme2DiaLabel%0A%7B%0A%20%20hint%3AQuery%20hint%3Aoptimizer%20%22None%22.%0A%20%20VALUES%20%3Flexeme%20%7B%20wd%3A${ l }%20%7D%20%0A%20%20%3Flexeme%20dct%3Alanguage%20%3Flexemelang%20.%0A%20%20%7B%0A%20%20%20%20%3Flexeme%20(wdt%3AP5238%20%7C%20wdt%3AP5191%20%7C%20wdt%3AP144%20%7C%20wdt%3AP5920%20%7C%20wdt%3AP11577)%2B%20%7C%20(%5Ewdt%3AP5238%20%7C%20%5Ewdt%3AP5191%20%7C%20%5Ewdt%3AP144%20%7C%20%5Ewdt%3AP5920%20%7C%20%5Ewdt%3AP11577)%2B%20%3Flexeme1%20%2C%20%3Flexeme2%20.%20%0A%20%20%20%20%3Flexeme1%20wdt%3AP5238%20%7C%20wdt%3AP5191%20%7C%20wdt%3AP144%20%7C%20wdt%3AP5920%20%7C%20wdt%3AP11577%20%3Flexeme2%20.%0A%20%20%20%20OPTIONAL%7B%3Flexeme1%20(p%3AP5191%20%7C%20p%3AP144%20%7C%20p%3AP5920%20%7C%20p%3AP11577)%20%3Fabs%20.%20%3Fabs%20(ps%3AP5238%20%7C%20ps%3AP5191%20%7C%20ps%3AP144%20%7C%20ps%3AP5920%20%7C%20ps%3AP11577)%20%3Flexeme2%20.%20%3Fabs%20pq%3AP5980%20%3Fl2sensse%20.%7D%0A%20%20%20%20OPTIONAL%7B%3Flexeme2%20(p%3AP5191%20%7C%20p%3AP144%20%7C%20p%3AP5920%20%7C%20p%3AP11577)%20%3Fabs%20.%20%3Fabs%20(ps%3AP5238%20%7C%20ps%3AP5191%20%7C%20ps%3AP144%20%7C%20ps%3AP5920%20%7C%20ps%3AP11577)%20%3Flexeme1%20.%20%3Fabs%20pq%3AP5980%20%3Fl1sensse%20.%7D%0A%0A%20%20%20%20%3Flexeme1%20ontolex%3Asense%20%3Fl1sensse2%20.%0A%20%20%20%20OPTIONAL%7B%3Flexeme2%20ontolex%3Asense%20%3Fl2sensse2%20.%7D%0A%20%20%7D%0A%20%20UNION%0A%20%20%7B%0A%20%20%20%20BIND(%3Flexeme%20AS%20%3Flexeme1)%0A%20%20%20%20%3Flexeme%20%20wdt%3AP5238%20%7C%20wdt%3AP5191%20%7C%20wdt%3AP144%20%7C%20wdt%3AP5920%20%7C%20wdt%3AP11577%20%7C%20ontolex%3Asense%20%2F%20wdt%3AP5972%20%2F%20%5Eontolex%3Asense%20%3Flexeme2%20.%0A%20%20%20%20OPTIONAL%7B%3Flexeme1%20(p%3AP5238%20%7C%20p%3AP5191%20%7C%20p%3AP144%20%7C%20p%3AP5920%20%7C%20p%3AP11577)%20%3Fabs%20.%20%3Fabs%20(ps%3AP5238%20%7C%20ps%3AP5191%20%7C%20ps%3AP144%20%7C%20ps%3AP5920%20%7C%20ps%3AP11577)%20%3Flexeme2%20.%20%3Fabs%20pq%3AP5980%20%3Fl2sensse%20.%7D%0A%20%20%20%20%23OPTIONAL%7B%3Flexeme2%20(%20p%3AP5191%20%7C%20p%3AP144%20%7C%20p%3AP5920%20%7C%20p%3AP11577)%20%2F%20pq%3AP5980%20%3Fl2sensse%20.%7D%0A%20%20%20%20%3Flexeme1%20ontolex%3Asense%20%3Fl1sensse2%20.%0A%20%20%20%20OPTIONAL%7B%3Flexeme2%20ontolex%3Asense%20%3Fl2sensse2%20.%7D%0A%20%20%7D%0A%20%20UNION%0A%20%20%7B%0A%20%20%20%20BIND(%3Flexeme%20AS%20%3Flexeme2)%0A%20%20%20%20%3Flexeme1%20wdt%3AP5238%20%7C%20wdt%3AP5191%20%7C%20wdt%3AP144%20%7C%20wdt%3AP5920%20%7C%20wdt%3AP11577%20%20%3Flexeme%20.%0A%20%20%20%20OPTIONAL%7B%20%3Flexeme1%20(p%3AP5238%20%7C%20p%3AP5191%20%7C%20p%3AP144%20%7C%20p%3AP5920%20%7C%20p%3AP11577)%20%3Fabs%20.%20%3Fabs%20(ps%3AP5238%20%7C%20ps%3AP5191%20%7C%20ps%3AP144%20%7C%20ps%3AP5920%20%7C%20ps%3AP11577)%20%3Flexeme2%20.%20%3Fabs%20pq%3AP5980%20%3Fl2sensse%20.%7D%0A%20%20%20%20%23OPTIONAL%7B%3Flexeme2%20(p%3AP5238%20%7C%20p%3AP5191%20%7C%20p%3AP144%20%7C%20p%3AP5920%20%7C%20p%3AP11577)%20%2F%20pq%3AP5980%20%3Fl2sensse%20.%7D%0A%20%20%20%20%3Flexeme1%20ontolex%3Asense%20%3Fl1sensse2%20.%0A%20%20%20%20OPTIONAL%7B%3Flexeme2%20ontolex%3Asense%20%3Fl2sensse2%20.%7D%0A%20%20%7D%0A%20%20BIND(COALESCE(IF(bound(%3Fl1sensse)%2C%3Fl1sensse%2C%3Fl1sensse2)%2C%22%22)%20AS%20%3Fl1sense)%0A%20%20BIND(COALESCE(IF(bound(%3Fl2sensse)%2C%3Fl2sensse%2C%3Fl2sensse2)%2C%22%22)%20AS%20%3Fl2sense)%0A%20%20OPTIONAL%20%7B%20%3Fl1sense%20(wdt%3AP5137%7Cwdt%3AP9970)%3F%20%2F%20wdt%3AP18%20%3Flexeme1Image%20%7D%0A%20%20OPTIONAL%20%7B%20%3Fl1sense%20wdt%3AP5972%20%3Flexeme1T%20.%20%3Flexeme1T%20skos%3Adefinition%20%3Flexeme1TDef%20.%20FILTER(lang(%3Flexeme1TDef)%3D%22en%22)%20%0A%20%20%20%20%20%20%20%20%20%20%20%20%3Flexeme1Trans%20ontolex%3Asense%20%3Flexeme1T%20.%20%3Flexeme1Trans%20wikibase%3Alemma%20%3Flexeme1TranssLemma%20.%20%3Flexeme1Trans%20dct%3Alanguage%20%3Flexeme1Translang%20.%0A%20%20%20%20%20%20%20%20%20%20%20%20%3Flexeme1Translang%20rdfs%3Alabel%20%3Flexeme1TranslangLabel%20.%20FILTER(lang(%3Flexeme1TranslangLabel)%3D%22en%22)%20%0A%20%20%20%20%20%20%20%20%20%20%20%20%20OPTIONAL%20%7B%3Flexeme1Trans%20wdt%3AP7481%20%3Flexeme1TransDia%20.%20%3Flexeme1TransDia%20rdfs%3Alabel%20%3Flexeme1TransDiaLabel%20.%20FILTER(lang(%3Flexeme1TransDiaLabel)%3D%22en%22)%7D%0A%20%20%20%20%20%20%20%20%20%20%20%20BIND(COALESCE(CONCAT(%22%20(%22%2C%3Flexeme1TDef%2C%22)%20%5B%22%2C%3Flexeme1TranslangLabel%2C%22%5D%22%2CCOALESCE(CONCAT(%22%20%3C%22%2C%3Flexeme1TransDiaLabel%2C%22%3E%22)%2C%22%22))%2C%22%22)%20AS%20%3Flexeme1TransLabelBP)%7D%0A%20%20OPTIONAL%20%7B%20%3Fl2sense%20wdt%3AP5972%20%3Flexeme2T%20.%20%3Flexeme2T%20skos%3Adefinition%20%3Flexeme2TDef%20.%20FILTER(lang(%3Flexeme2TDef)%3D%22en%22)%20%0A%20%20%20%20%20%20%20%20%20%20%20%20%3Flexeme2Trans%20ontolex%3Asense%20%3Flexeme2T%20.%20FILTER(%3Flexeme2Trans%3D%3Flexeme2)%20%3Flexeme2Trans%20wikibase%3Alemma%20%3Flexeme2TranssLemma%20.%20%3Flexeme2Trans%20dct%3Alanguage%20%3Flexeme2Translang%20.%0A%20%20%20%20%20%20%20%20%20%20%20%20%3Flexeme2Translang%20rdfs%3Alabel%20%3Flexeme2TranslangLabel%20.%20FILTER(lang(%3Flexeme2TranslangLabel)%3D%22en%22)%20%0A%20%20%20%20%20%20%20%20%20%20%20%20OPTIONAL%20%7B%3Flexeme2Trans%20wdt%3AP7481%20%3Flexeme2TransDia%20.%20%3Flexeme2TransDia%20rdfs%3Alabel%20%3Flexeme2TransDiaLabel%20.%20FILTER(lang(%3Flexeme2TransDiaLabel)%3D%22en%22)%7D%0A%20%20%20%20%20%20%20%20%20%20%20%20BIND(COALESCE(CONCAT(%22%20(%22%2C%3Flexeme2TDef%2C%22)%20%5B%22%2C%3Flexeme2TranslangLabel%2C%22%5D%22%2CCOALESCE(CONCAT(%22%20%3C%22%2C%3Flexeme2TransDiaLabel%2C%22%3E%22)%2C%22%22))%2C%22%22)%20AS%20%3Flexeme2TransLabelBP)%7D%0A%20%20OPTIONAL%20%7B%20%3Fl2sense%20(wdt%3AP5137%7Cwdt%3AP9970)%3F%20%2F%20wdt%3AP18%20%3Flexeme2Image%20%7D%0A%20%20OPTIONAL%20%7B%20%3Fl1sense%20skos%3Adefinition%20%3Flexeme1gloss%20.%20FILTER(lang(%3Flexeme1gloss)%3D%22en%22)%20%7D%0A%20%20OPTIONAL%20%7B%20%3Fl2sense%20skos%3Adefinition%20%3Flexeme2gloss%20.%20FILTER(lang(%3Flexeme2gloss)%3D%22en%22)%20%7D%0A%20%20OPTIONAL%20%7B%20%3Flexeme1%20wdt%3AP7481%20%3Flexeme1Dia%20.%20%3Flexeme1Dia%20rdfs%3Alabel%20%3Flexeme1DiaLabel%20.%20FILTER(lang(%3Flexeme1DiaLabel)%3D%22en%22)%20%7D%0A%20%20OPTIONAL%20%7B%20%3Flexeme2%20wdt%3AP7481%20%3Flexeme2Dia%20.%20%3Flexeme2Dia%20rdfs%3Alabel%20%3Flexeme2DiaLabel%20.%20FILTER(lang(%3Flexeme2DiaLabel)%3D%22en%22)%20%7D%0A%20%20%3Flexeme1%20wikibase%3Alemma%20%3Flexeme1Labell%20.%0A%20%20%3Flexeme1%20dct%3Alanguage%20%3Flexeme1lang%20.%0A%20%20%3Flexeme1lang%20rdfs%3Alabel%20%3Flexeme1langLabell%20.%0A%20%20%3Flexeme2%20wikibase%3Alemma%20%3Flexeme2Labell%20.%0A%20%20%3Flexeme2%20dct%3Alanguage%20%3Flexeme2lang%20.%0A%20%20%3Flexeme2lang%20rdfs%3Alabel%20%3Flexeme2langLabell%20.%0A%20%20FILTER(lang(%3Flexeme1langLabell)%3D%22en%22)%0A%20%20FILTER(lang(%3Flexeme2langLabell)%3D%22en%22)%0A%20%20%23FILTER(%3Flexeme1Trans!%3D%3Flexeme2Trans)%0A%20%20BIND(CONCAT(%22%20%5B%22%2C%3Flexeme1langLabell%2C%22%5D%22%2CCOALESCE(CONCAT(%22%20%3C%22%2C%3Flexeme1DiaLabel%2C%22%3E%22)%2C%22%22))%20AS%20%3Flexeme1langLabel)%0A%20%20BIND(CONCAT(%22%20%5B%22%2C%3Flexeme2langLabell%2C%22%5D%22%2CCOALESCE(CONCAT(%22%20%3C%22%2C%3Flexeme2DiaLabel%2C%22%3E%22)%2C%22%22))%20AS%20%3Flexeme2langLabel)%0A%20%20BIND(IF(bound(%3Flexeme1Trans)%20%20%2C%20%22778899%22%2C%20%22FFFFFF%22)%20AS%20%3Frgb)%0A%7D%0AGROUP%20BY%20%3Flexeme1%20%3Flexeme2%20%3Flexeme2Image%20%3Flexeme1Image%20%3Flexeme1gloss%20%3Flexeme2gloss%20%3Flexeme1langLabel%20%3Frgb%20%3Flexeme2langLabel%20%3Flexeme1TransLabelBP%20%3Flexeme2TransLabelBP%20%3Flexeme1Trans%20%3Flexeme2Trans%20%3Flexeme1DiaLabel%20%3Flexeme2DiaLabel%0A%7D%0ABIND(CONCAT(str(%3Flexeme1Llabel)%2CCOALESCE(CONCAT(%22%20(%22%2C%3Flexeme1gloss%2C%22)%22)%2C%22%22)%2Cstr(%3Flexeme1langLabel))%20AS%20%3Flexeme1Label)%0ABIND(CONCAT(str(%3Flexeme2Llabel)%2CCOALESCE(CONCAT(%22%20(%22%2C%3Flexeme2gloss%2C%22)%22)%2C%22%22)%2Cstr(%3Flexeme2langLabel))%20AS%20%3Flexeme2Label)%0ABIND(CONCAT(str(%3Flexeme1TransLabell)%2Cstr(%3Flexeme1TransLabelBP))%20AS%20%3Flexeme1TransLabel)%0ABIND(CONCAT(str(%3Flexeme2TransLabell)%2Cstr(%3Flexeme2TransLabelBP))%20AS%20%3Flexeme2TransLabel)%0A%7D"></iframe></div>