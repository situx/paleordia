---
layout: base
---

<script type="text/javascript">
lexemesSparql =`

SELECT ?lexeme (GROUP_CONCAT(DISTINCT ?lexemeLab; SEPARATOR = " // ") AS ?lexemeLabel) 
(GROUP_CONCAT(DISTINCT ?sensee; SEPARATOR = " // ") AS ?senses)
(GROUP_CONCAT(DISTINCT ?senselabel; SEPARATOR = " // ") AS ?sensesLabel)
?lexical_category ?lexical_categoryLabel ?refnumber ?refs WITH {
  SELECT ?lexeme ?refnumber (COUNT(?dict) as ?refs) WHERE {
  ?lexeme rdf:type ontolex:LexicalEntry;
    dct:language wd:${ l } ;
    p:P1343 ?lex . ?lex ps:P1343 wd:${ q } .
    ?lexeme wdt:P1343 ?dict .
    OPTIONAL{wd:${ q } p:P724 ?inetarchive_stat . ?inetarchive_stat ps:P724 ?inetarchive . OPTIONAL{?inetarchive_stat pq:P8330 ?offset .}}
      OPTIONAL { ?lex pq:P478 ?refvol. }
	  OPTIONAL { ?lex pq:P1810 ?refnum. }
	  OPTIONAL { ?lex pq:P7421 ?refline. }
	  OPTIONAL { ?lex pq:P304 ?refpage. }
	  OPTIONAL { ?lex pq:P3903 ?refcol. }
	  BIND(COALESCE(CONCAT(" ",?refnum), "") AS ?thenum)
	  BIND(COALESCE(CONCAT(" [v. ", ?refvol,"]"), "") AS ?thevol)
	  BIND(COALESCE(CONCAT(" [p. ", ?refpage, COALESCE(?refcol,""), "]"), "") AS ?thepage)
	  BIND(COALESCE(CONCAT(" [l. ", ?refline, "]"), "") AS ?theline)
	  BIND(IF(bound(?offset), xsd:integer(?refpage)+xsd:integer(?offset), ?refpage) AS ?realpage)
	  BIND(COALESCE(CONCAT(" <a target='_blank' href='https://archive.org/details/",?inetarchive,COALESCE(CONCAT("/page/n",STR(?realpage),"/mode/1up?view=theater"),""),"'>↗</a>"),"") AS ?sourceUrl)
	  BIND(CONCAT(?thevol, ?thepage, ?theline, ?thenum,?sourceUrl) AS ?refnumber)
  }GROUP BY ?lexeme ?refnumber
  } AS %i WHERE {
      INCLUDE %i 
   ?lexeme wikibase:lemma ?lexemeLab ;
	wikibase:lexicalCategory ?lexical_category .
  ?lexical_category rdfs:label ?lexical_categoryLabel.
  FILTER(LANG(?lexical_categoryLabel) = "en")
  OPTIONAL {
    ?lexeme ontolex:sense ?senseitem.   
    OPTIONAL {
      ?senseitem wdt:P5137|wdt:P9970 ?sensee.
      ?sensee rdfs:label ?senseLab.
      FILTER(LANG(?senseLab)="en")
	  BIND(COALESCE(REPLACE(STR(?sensee), "http://www.wikidata.org/entity/", ""), "") AS ?sensse)
      BIND(COALESCE(REPLACE(STR(?senseitem), "http://www.wikidata.org/entity/", ""), "") AS ?sensel)
    }
	OPTIONAL {
		FILTER("${ displaylang }"!="en")
		?senseitem skos:definition ?glosss.
		FILTER(LANG(?glosss)="${ displaylang }")
		BIND(CONCAT(?glosss,"@${ displaylang }") AS ?gloss)
	}
	OPTIONAL {
		?senseitem skos:definition ?gloss.
		FILTER(LANG(?gloss)="en")
	}
    BIND(CONCAT(?gloss, COALESCE(CONCAT(" [", ?senseLab, " (", ?sensse, ")", "] <a target='_blank' href='../ls/?l=",REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", ""),"&s=",STRAFTER(STR(?sensel),"-"),"&qLabel=",STR(?senseLab),"'>&#8599;</a>"),"")) AS ?senselabel)
  }
}
GROUP BY ?lexeme ?lexical_category ?lexical_categoryLabel ?refnumber ?refs
LIMIT 15000
`


lexemesSparqlP=`
  SELECT DISTINCT ?lexeme (GROUP_CONCAT(DISTINCT ?lexemeLab; separator=" // ") AS ?lexemeLabel) ?lexical_category ?lexical_categoryLabel (GROUP_CONCAT(DISTINCT ?senseval; separator=" / ") as ?sense)
	(GROUP_CONCAT(DISTINCT ?senselabel; separator=" / ") as ?senseLabel) (CONCAT(?formaturl,?refnumber) AS ?refnumberUrl) ?refnumber WHERE {
	 {
   SELECT ?lexeme ?lexemeLab ?refnumber ?lexical_category ?lexical_categoryLabel WHERE {
	?lexeme rdf:type ontolex:LexicalEntry;
		dct:language wd:${ l };
		wikibase:lemma ?lexemeLab ;    
        wdt:${ p } ?refnumber ; 		
		wikibase:lexicalCategory ?lexical_category .
    ?lexical_category rdfs:label ?lexical_categoryLabel .
    FILTER (LANG(?lexical_categoryLabel) = "en")
	}}
	OPTIONAL {
	    ?idpropp wikibase:directClaim wdt:${ p } .
		?idpropp wdt:P1630 ?formatur .
		BIND(REPLACE(STR(?formatur), "\\\\$1", "", "i") AS ?formaturl)
	}
	
	OPTIONAL { ?lexeme ontolex:sense ?sense . ?sense skos:definition ?gloss .  FILTER(LANG(?gloss)= "en") 
	  OPTIONAL {?sense wdt:P5137|wdt:P9970 ?senseval . ?senseval rdfs:label ?senselab .  FILTER(LANG(?senselab)= "en")} 
	  BIND(CONCAT(?gloss, COALESCE(CONCAT(" [", ?senselab, " (", REPLACE(STR(?senseval), "http://www.wikidata.org/entity/", ""), ")", "]"),"")) AS ?senselabel) 
	}     
  }
  GROUP BY ?lexeme ?refnumber ?lexical_category ?lexical_categoryLabel ?formaturl
  ORDER BY (?refnumber)
`

 $(document).ready(function() {
  if(typeof(p)!=='undefined'&& p!=null && p!="" && (typeof(q)==='undefined' || q==null || q=="")){    
 	sparqlToDataTable(lexemesSparqlP, "#lexemes", {
	 'linkPrefixes': {
		'lexeme': '../l'
	 },
	 linkParams: {
		lexeme: '&qb='+q+'&displaylang='+displaylang
	 },
	 pBar:"progressbar",
	 pBarLabel:"progress-label",
	 highlight:highlight,
	 highlightcol:3
	 }); 
  }else{
	sparqlToDataTable(lexemesSparql, "#lexemes", {
	 'linkPrefixes': {
		'lexeme': '../l'
	 },
	 linkParams: {
		lexeme: '&qb='+q+'&displaylang='+displaylang
	 },
	 pBar:"progressbar",
	 pBarLabel:"progress-label",
	 highlight:highlight,
	 highlightcol:3
	 });
   }
 });

</script>

<div class="language-entries">
  <h1>Lexemes by reference: ${ qLabel } (${ q }${ p })</h1>

  <a title="Back"
     role="button"
     class="btn btn-outline-dark btn-sm"
     href="../language?q=${ l }&qLabel=${ lLabel }">←</a>
  <a title="View and edit item in Wikidata"
     role="button"
     class="btn btn-outline-dark btn-sm"
     href="https://www.wikidata.org/wiki/${ q }${ p }">Wikidata ↗</a>
  <a title="View item in Reasonator"
     role="button"
     class="btn btn-outline-dark btn-sm"
     href="https://reasonator.toolforge.org/?&q=${ q }${ p }">Reasonator ↗</a>
  <a title="View item as a Scholia topic"
     role="button"
     class="btn btn-outline-dark btn-sm"
     href="https://scholia.toolforge.org/topic/${ q }${ p }">Scholia ↗</a>


  <h2>Lexemes referenced by this resource</h2>
  <div id="progressbar"><span class="progress-label" id="progress-label">Loading...</span></div>
  <table class="table table-hover" id="lexemes"></table>
  

</div>




