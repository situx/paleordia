---
layout: base
---

<script type="text/javascript">
lexemesSparql =`
SELECT ?lexeme ?lexemeLabel ?lexical_category ?lexical_categoryLabel ?sense ?senseLabel ?refnumber (COUNT(distinct ?refs)+COUNT(distinct ?idprop) as ?refcount) WITH {   
	SELECT DISTINCT ?lexeme (GROUP_CONCAT(DISTINCT ?lexemeLab; separator=" // ") AS ?lexemeLabel) ?lexical_category ?lexical_categoryLabel 	(GROUP_CONCAT(DISTINCT ?sensee; separator=" / ") as ?sense)
	(GROUP_CONCAT(DISTINCT ?senselabel; separator=" / ") as ?senseLabel) ?refnumber WHERE {
	  ?lexeme rdf:type ontolex:LexicalEntry;
		dct:language wd:${ l };
        p:P1343 ?lex ;
		wikibase:lemma ?lexemeLab ;      
		wikibase:lexicalCategory ?lexical_category .
      ?lexical_category rdfs:label ?lexical_categoryLabel .
      ?lex ps:P1343  wd:${ q } ;
	  FILTER (LANG(?lexical_categoryLabel) = "en")
	  OPTIONAL{wd:${ q } p:P724 ?inetarchive_stat . ?inetarchive_stat ps:P724 ?inetarchive . OPTIONAL{?inetarchive_stat pq:P8330 ?offset .}}
      OPTIONAL { ?lex pq:P478 ?refvol. }
	  OPTIONAL { ?lex pq:P1810 ?refnum. }
	  OPTIONAL { ?lex pq:P7421 ?refline. }
	  OPTIONAL { ?lex pq:P304 ?refpage. }
	  OPTIONAL { ?lex pq:P3903 ?refcol. }
	  BIND(COALESCE(CONCAT(" ",?refnum), "") AS ?thenum)
	  BIND(COALESCE(CONCAT(" [v. ", ?refvol,"]"), "") AS ?thevol)
	  BIND(COALESCE(CONCAT(" [p. ", ?refpage, COALESCE(?refcol,""), "]"), "") AS ?thepage)
	  BIND(COALESCE(CONCAT(" [l. ", ?refline, "]"), "") AS ?theline)
	  BIND(IF(bound(?offset), xsd:integer(?refpage)+xsd:integer(?offset), ?refpage) AS ?realpage)
	  BIND(COALESCE(CONCAT(" <a target='_blank' href='https://archive.org/details/",?inetarchive,COALESCE(CONCAT("/page/n",STR(?realpage),"/mode/2up/"),""),"'>↗</a>"),"") AS ?sourceUrl)
	  BIND(CONCAT(?thevol, ?thepage, ?theline, ?thenum,?sourceUrl) AS ?refnumber)
      OPTIONAL { 
		?lexeme ontolex:sense ?senseitem.
		OPTIONAL {
		  ?senseitem wdt:P5137|wdt:P9970 ?sensee.
		  ?sensee rdfs:label ?senseLab.
		  FILTER(LANG(?senseLab)="en")
		  BIND(COALESCE(REPLACE(STR(?sensee), "http://www.wikidata.org/entity/", ""), "") AS ?sensse)
		}
		OPTIONAL {
			FILTER("en"!="en")
			?senseitem skos:definition ?glosss.
			FILTER(LANG(?glosss)="en")
			BIND(CONCAT(?glosss,"@en") AS ?gloss)
		}
		OPTIONAL {
			?senseitem skos:definition ?gloss.
			FILTER(LANG(?gloss)="en")
		}
		BIND(CONCAT(?gloss, COALESCE(CONCAT(" [", ?senseLab, " (", REPLACE(STR(?sensee), "http://www.wikidata.org/entity/", ""), ")", "]"),"")) AS ?senselabel)
		}     
	}
	GROUP BY ?lexeme ?refnumber ?lexical_category ?lexical_categoryLabel
	ORDER BY (?refnumber)
	} AS %i WHERE {
      INCLUDE %i
    ?lexeme wdt:P1343 ?refs .
    OPTIONAL{?lexeme ?idprop ?id . 
     ?idpropp wikibase:directClaim ?idprop ; wikibase:propertyType wikibase:ExternalId .
    }
  }
GROUP BY ?lexeme ?lexemeLabel ?refnumber ?lexical_category ?lexical_categoryLabel ?sense ?senseLabel
`


lexemesSparqlP=`
  SELECT DISTINCT ?lexeme (GROUP_CONCAT(DISTINCT ?lexemeLab; separator=" // ") AS ?lexemeLabel) ?lexical_category ?lexical_categoryLabel (GROUP_CONCAT(DISTINCT ?senseval; separator=" / ") as ?sense)
	(GROUP_CONCAT(DISTINCT ?senselabel; separator=" / ") as ?senseLabel) (CONCAT(?formaturl,?refnumber) AS ?refnumberUrl) ?refnumber WHERE {
	 {
   SELECT ?lexeme ?lexemeLab ?refnumber ?lexical_category ?lexical_categoryLabel WHERE {
	?lexeme rdf:type ontolex:LexicalEntry;
		dct:language wd:${ l };
		wikibase:lemma ?lexemeLab ;    
        wdt:${ p } ?refnumber ; 		
		wikibase:lexicalCategory ?lexical_category .
    ?lexical_category rdfs:label ?lexical_categoryLabel .
    FILTER (LANG(?lexical_categoryLabel) = "en")
	}}
	OPTIONAL {
	    ?idpropp wikibase:directClaim wdt:${ p } .
		?idpropp wdt:P1630 ?formatur .
		BIND(REPLACE(STR(?formatur), "\\\\$1", "", "i") AS ?formaturl)
	}
	
	OPTIONAL { ?lexeme ontolex:sense ?sense . ?sense skos:definition ?gloss .  FILTER(LANG(?gloss)= "en") 
	  OPTIONAL {?sense wdt:P5137|wdt:P9970 ?senseval . ?senseval rdfs:label ?senselab .  FILTER(LANG(?senselab)= "en")} 
	  BIND(CONCAT(?gloss, COALESCE(CONCAT(" [", ?senselab, " (", REPLACE(STR(?senseval), "http://www.wikidata.org/entity/", ""), ")", "]"),"")) AS ?senselabel) 
	}     
  }
  GROUP BY ?lexeme ?refnumber ?lexical_category ?lexical_categoryLabel ?formaturl
  ORDER BY (?refnumber)
`

 $(document).ready(function() {
  if(typeof(p)!=='undefined'&& p!=null && p!="" && (typeof(q)==='undefined' || q==null || q=="")){    
 	sparqlToDataTable(lexemesSparqlP, "#lexemes", {
	 'linkPrefixes': {
		'lexeme': '../l'
	 },
	 linkParams: {
		lexeme: '&qb='+q+'&displaylang='+displaylang
	 },
	 pBar:"progressbar",
	 pBarLabel:"progress-label",
	 highlight:highlight,
	 highlightcol:3
	 }); 
  }else{
	sparqlToDataTable(lexemesSparql, "#lexemes", {
	 'linkPrefixes': {
		'lexeme': '../l'
	 },
	 linkParams: {
		lexeme: '&qb='+q+'&displaylang='+displaylang
	 },
	 pBar:"progressbar",
	 pBarLabel:"progress-label",
	 highlight:highlight,
	 highlightcol:3
	 });
   }
 });

</script>

<div class="language-entries">
  <h1>Lexemes by reference: ${ qLabel } (${ q }${ p })</h1>

  <a title="Back"
     role="button"
     class="btn btn-outline-dark btn-sm"
     href="../language?q=${ l }&qLabel=${ lLabel }">←</a>
  <a title="View and edit item in Wikidata"
     role="button"
     class="btn btn-outline-dark btn-sm"
     href="https://www.wikidata.org/wiki/${ q }${ p }">Wikidata ↗</a>
  <a title="View item in Reasonator"
     role="button"
     class="btn btn-outline-dark btn-sm"
     href="https://reasonator.toolforge.org/?&q=${ q }${ p }">Reasonator ↗</a>
  <a title="View item as a Scholia topic"
     role="button"
     class="btn btn-outline-dark btn-sm"
     href="https://scholia.toolforge.org/topic/${ q }${ p }">Scholia ↗</a>


  <h2>Lexemes referenced by this resource</h2>
  <div id="progressbar"><span class="progress-label" id="progress-label">Loading...</span></div>
  <table class="table table-hover" id="lexemes"></table>
  

</div>




